// ------------------------ Libraries ------------------------
#include <DHT.h>
#include <WiFi.h>
#include <PZEM004Tv30.h>
#include <PubSubClient.h>
#include <LiquidCrystal_I2C.h>

// ------------------------ WiFi Credentials ------------------------
const char* ssid     = "motoedge50fusion_8994";
const char* password = "9876543210";

// ------------------------ MQTT Settings ------------------------
const char* mqtt_server = "broker.emqx.io";
const int mqtt_port     = 1883;
const char* client_id   = "ESP32PowerClient";

// ------------------------ MQTT Topics ------------------------
#define TEMP_TOPIC         "esp32/home584/temp"
#define HUMI_TOPIC         "esp32/home584/humi"
#define VOLTAGE_TOPIC      "esp32/home584/voltage"
#define CURRENT_TOPIC      "esp32/home584/current"
#define POWER_TOPIC        "esp32/home584/power"
#define ENERGY_TOPIC       "esp32/home584/energy"
#define FREQUENCY_TOPIC    "esp32/home584/frequency"
#define PF_TOPIC           "esp32/home584/pf"
#define COST_TOPIC         "esp32/home584/cost"
#define POWER_STATUS_TOPIC "esp32/home584/power_status"
#define LED1_CONTROL_TOPIC "esp32/home584/led1"
#define LED2_CONTROL_TOPIC "esp32/home584/led2"
#define LED1_STATUS_TOPIC  "esp32/home584/led1/status"
#define LED2_STATUS_TOPIC  "esp32/home584/led2/status"
#define RESET_ENERGY_TOPIC "esp32/home584/reset_energy"

// ------------------------ Pin Configuration ------------------------
#define LED1_PIN   19
#define LED2_PIN   18
#define DHTPIN     27
#define DHTTYPE    DHT22
#define PZEM_RX_PIN 16
#define PZEM_TX_PIN 17

// ------------------------ Global Variables ------------------------
bool led1State = LOW, led2State = LOW;
bool powerWasOn = true;

DHT dht(DHTPIN, DHTTYPE);
LiquidCrystal_I2C lcd(0x27, 20, 4);
PZEM004Tv30 pzem(Serial2, PZEM_RX_PIN, PZEM_TX_PIN);
WiFiClient espClient;
PubSubClient client(espClient);

// ------------------------ Custom Characters ------------------------
byte wifiConnectedChar[8] = {
  B00000, B00000, B01110, B11111,
  B01110, B00100, B00000, B00000
};

byte wifiDisconnectedChar[8] = {
  B00000, B00001, B01110, B11111,
  B01110, B10100, B00000, B00000
};

// ------------------------ Timing Variables ------------------------
unsigned long lastUpdate = 0;
const unsigned long updateInterval = 2000;
unsigned long lastWiFiCheck = 0;
const unsigned long wifiRetryInterval = 3000;

// ------------------------ Function: Update WiFi Icon ------------------------
void updateWiFiSymbol() {
  lcd.setCursor(19, 0);
  lcd.write(WiFi.status() == WL_CONNECTED ? byte(0) : byte(1));
}

// ------------------------ Function: Calculate Energy Cost ------------------------
float calculateCost(float energy) {
  float cost = 0;
  if (energy <= 500) {
    if (energy > 400) cost += (energy - 400) * 6.0, energy = 400;
    if (energy > 200) cost += (energy - 200) * 4.5, energy = 200;
    if (energy > 100) cost += (energy - 100) * 2.25, energy = 100;
  } else {
    if (energy > 1000) cost += (energy - 1000) * 11.0, energy = 1000;
    if (energy > 800)  cost += (energy - 800) * 10.0, energy = 800;
    if (energy > 600)  cost += (energy - 600) * 9.0,  energy = 600;
    if (energy > 500)  cost += (energy - 500) * 8.0,  energy = 500;
    if (energy > 400)  cost += (energy - 400) * 60.0, energy = 400;
    if (energy > 100)  cost += (energy - 100) * 4.5,  energy = 100;
  }
  return cost;
}

// ------------------------ MQTT Callback ------------------------
void callback(char* topic, byte* payload, unsigned int length) {
  String msg = "";
  for (uint8_t i = 0; i < length; i++) msg += (char)payload[i];

  if (String(topic) == LED1_CONTROL_TOPIC) {
    led1State = (msg == "1");
    digitalWrite(LED1_PIN, !led1State);
    client.publish(LED1_STATUS_TOPIC, led1State ? "1" : "0", true);
  }
  else if (String(topic) == LED2_CONTROL_TOPIC) {
    led2State = (msg == "1");
    digitalWrite(LED2_PIN, !led2State);
    client.publish(LED2_STATUS_TOPIC, led2State ? "1" : "0", true);
  }
  else if (String(topic) == RESET_ENERGY_TOPIC && msg == "1") {
    pzem.resetEnergy();
    Serial.println("Energy counter reset via MQTT");
    lcd.clear();
    lcd.setCursor(2, 1);
    lcd.print("Energy Reset Done!");
    delay(1500);
  }
}

// ------------------------ MQTT Reconnect ------------------------
void reconnect() {
  if (WiFi.status() != WL_CONNECTED) return;

  while (!client.connected()) {
    Serial.print("Connecting to MQTT...");
    lcd.clear();
    lcd.setCursor(0, 0); lcd.print("WiFi Connected!");
    lcd.setCursor(0, 1); lcd.print("IP:"); lcd.print(WiFi.localIP());
    lcd.setCursor(0, 2); lcd.print("Online Mode");
    lcd.setCursor(0, 3); lcd.print("Connecting to MQTT..");

    updateWiFiSymbol();
    delay(2000);

    if (client.connect(client_id)) {
      Serial.println("connected.");
      client.subscribe(LED1_CONTROL_TOPIC);
      client.subscribe(LED2_CONTROL_TOPIC);
      client.subscribe(RESET_ENERGY_TOPIC);
    } else {
      Serial.println("MQTT failed, retrying in 5s");
      delay(5000);
    }

    lcd.clear();
  }
}

// ------------------------ Setup ------------------------
void setup() {
  Serial.begin(9600);
  dht.begin();
  lcd.begin();
  lcd.backlight();
  lcd.createChar(0, wifiConnectedChar);
  lcd.createChar(1, wifiDisconnectedChar);

  pinMode(LED1_PIN, OUTPUT);
  pinMode(LED2_PIN, OUTPUT);
  digitalWrite(LED1_PIN, HIGH);
  digitalWrite(LED2_PIN, HIGH);

  WiFi.begin(ssid, password);
  lcd.setCursor(0, 0); lcd.print("Smart Energy Meter");
  lcd.setCursor(0, 2); lcd.print("WiFi is");
  lcd.setCursor(0, 3); lcd.print("Connecting");
  updateWiFiSymbol();

  unsigned long wifiStart = millis();
  const unsigned long wifiTimeout = 30000;  // 30 seconds
  unsigned long lastDotUpdate = 0;
  int dotCount = 0;

  while (WiFi.status() != WL_CONNECTED && millis() - wifiStart < wifiTimeout) {
    int remainingTime = (wifiTimeout - (millis() - wifiStart)) / 1000;

    lcd.setCursor(0, 1);
    lcd.print("Offline Mode in   ");
    lcd.setCursor(17, 1);
    lcd.printf("%2ds", remainingTime);
    updateWiFiSymbol();

    if (millis() - lastDotUpdate >= 300) {
      lcd.setCursor(10 + dotCount, 3); lcd.print(".");
      dotCount++;
      if (dotCount >= 5) {
        for (int i = 0; i < 5; i++) lcd.setCursor(10 + i, 3), lcd.print(" ");
        dotCount = 0;
      }
      lastDotUpdate = millis();
    }

    if (WiFi.status() == WL_CONNECTED) break;
  }

  if (WiFi.status() != WL_CONNECTED) {
    lcd.clear();
    lcd.setCursor(0, 0); lcd.print("WiFi Failed!");
    lcd.setCursor(0, 1); lcd.print("Offline Mode");
    updateWiFiSymbol();
    delay(2500);
  }

  lcd.clear();
  client.setServer(mqtt_server, mqtt_port);
  client.setCallback(callback);
}

// ------------------------ Loop ------------------------
void loop() {
  if (WiFi.status() != WL_CONNECTED && millis() - lastWiFiCheck >= wifiRetryInterval) {
    WiFi.begin(ssid, password);
    Serial.println("WiFi not connected. Attempting reconnection...");
    lastWiFiCheck = millis();
  }

  updateWiFiSymbol();

  if (!client.connected()) reconnect();
  client.loop();

  if (millis() - lastUpdate >= updateInterval) {
    lastUpdate = millis();

    float v   = pzem.voltage();
    float a   = pzem.current();
    float w   = pzem.power();
    float kWh = pzem.energy();
    float hz  = pzem.frequency();
    float pf  = pzem.pf();
    float t   = dht.readTemperature();
    float h   = dht.readHumidity();
    float cost = calculateCost(kWh);

    if (isnan(v) || isnan(a) || isnan(w) || isnan(kWh) || isnan(hz) || isnan(pf)) {
      if (powerWasOn) {
        client.publish(POWER_STATUS_TOPIC, "POWER_OFF", true);
        powerWasOn = false;
      }
      v = a = w = kWh = hz = pf = 0;
    } else {
      if (!powerWasOn) {
        client.publish(POWER_STATUS_TOPIC, "POWER_ON", true);
        powerWasOn = true;
      }
    }

    // MQTT publish
    client.publish(TEMP_TOPIC,     String(t, 1).c_str(), true);
    client.publish(HUMI_TOPIC,     String(h, 0).c_str(), true);
    client.publish(VOLTAGE_TOPIC,  String(v, 2).c_str(), true);
    client.publish(CURRENT_TOPIC,  String(a, 3).c_str(), true);
    client.publish(POWER_TOPIC,    String(w, 2).c_str(), true);
    client.publish(ENERGY_TOPIC,   String(kWh, 3).c_str(), true);
    client.publish(FREQUENCY_TOPIC,String(hz, 1).c_str(), true);
    client.publish(PF_TOPIC,       String(pf, 2).c_str(), true);
    client.publish(COST_TOPIC,     String(cost, 0).c_str(), true);

    // LCD update
    lcd.setCursor(0, 0); lcd.printf("T:%.1f*C H:%.0f%%", t, h);
    lcd.setCursor(0, 1); lcd.printf("V:%.1fV I:%.3fA", v, a);
    lcd.setCursor(0, 2); lcd.printf("P:%.2fW E:%.3fkWh", w, kWh);
    lcd.setCursor(0, 3); lcd.printf("F:%.0fHz PF:%.2f Rs:%.0f", hz, pf, cost);
  }
}

